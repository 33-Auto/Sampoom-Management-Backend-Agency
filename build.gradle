plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.sampoom'
version = '0.0.1-SNAPSHOT'
description = 'Sampoom Backend Agency'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // --- Spring ---
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // --- JWT ---
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'


    // --- Lombok ---
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // --- DB ---
    runtimeOnly 'org.postgresql:postgresql'

    // --- Feign ---
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // --- Swagger ---
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    // --- QueryDSL (수동 세팅) ---
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // --- Test ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.2"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

// QueryDSL 경로 지정
def querydslDir = layout.buildDirectory.dir("generated/querydsl")

tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorGeneratedSourcesDirectory = querydslDir.get().asFile
}

sourceSets {
    main {
        java {
            srcDir querydslDir
        }
    }
}

// 기본 설정 유지
bootJar {
    enabled = true
}
jar {
    enabled = false
}
